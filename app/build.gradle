apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'io.fabric'
apply plugin: 'androidx.navigation.safeargs'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 28

    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "ps.altariq.restaurant"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 10000 + versionMinor * 1000
        versionName "$versionMajor.$versionMinor.$versionPatch"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField('String', 'FULL_VERSION_NAME', "\"$versionName.$versionCode\"")
        resValue 'string', 'application_id', applicationId
        multiDexEnabled true
        renderscriptTargetApi 28
        renderscriptSupportModeEnabled true
    }

    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsEnabled: true]
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    androidExtensions {
        experimental = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // better output apk naming : {projectName}_{flavor(s)}_{buildType}_{versionName}_build_{buildVersion}.apk
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def sep = '_'
            def version = variant.versionName
            def flavors = ""
            variant.productFlavors.forEach { flavor ->
                flavors += "${flavor.name}${sep}"
            }
            outputFileName = "${rootProject.name}${sep}" +
                    "${flavors}" +
                    "${variant.buildType.name}${sep}" +
                    "${version}" + ".apk"
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    // android
    implementation "androidx.core:core-ktx:$versions.core_ktx"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$versions.lifecycle"
    implementation "android.arch.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "android.arch.navigation:navigation-ui-ktx:$versions.navigation"
    kapt "androidx.room:room-compiler:$versions.room"
    implementation "androidx.room:room-runtime:$versions.room"

    // dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"
    compileOnly "com.squareup.inject:assisted-inject-annotations-dagger2:$versions.assisted_inject"
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:$versions.assisted_inject"

    // reactive
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxkotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxbinding_kotlin"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:$versions.rx_preferences"
    implementation 'com.github.pwittchen:reactivenetwork-rx2:3.0.0'

    // network
    implementation 'com.microsoft.signalr:signalr:1.0.0'

    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation ("com.github.bumptech.glide:glide:4.8.0") {
        exclude group: "com.android.support"
    }

    // debug
    implementation "com.jakewharton.timber:timber:$versions.timber"


    // misc
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.6@aar') {
        transitive = true
    }
    implementation 'com.github.dmytrodanylyk.android-process-button:library:1.0.4'
    implementation 'com.github.f0ris.sweetalert:library:1.5.2'
    implementation 'com.mxn.soul:flowingdrawer-core:2.0.0'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'com.github.d-max:spots-dialog:1.1@aar'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.ms-square:etsyblur:0.2.1'
    implementation 'me.everything:overscroll-decor-android:1.0.4'


    implementation 'com.nineoldandroids:library:2.4.0'
    implementation ('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar'){
        transitive=true
    }
    implementation 'com.ramotion.cardslider:card-slider:0.3.1'
    implementation 'com.github.rubensousa:gravitysnaphelper:1.5'

    implementation 'me.everything:overscroll-decor-android:1.0.4'
    implementation 'com.stone.vega.library:VegaLayoutManager:1.0.6'

    implementation 'com.nightonke:boommenu:2.1.1'
    implementation 'com.github.medyo:fancybuttons:1.9.0'
    implementation 'com.github.DonMat:searchablespinner:v1.0.1'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.yanzhenjie.recyclerview:x:1.3.1'
}
repositories {
    mavenCentral()
}
